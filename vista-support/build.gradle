nexusRoot = "http://vhaislbll22.vha.DOMAIN.EXT:8081/nexus/content"
nexus = "${nexusRoot}/groups/public/"
nexusReleases = "${nexusRoot}/repositories/releases"
nexusSnapshots = "${nexusRoot}/repositories/snapshots"
springEbr = ["http://repository.springsource.com/maven/bundles/release", "http://repository.springsource.com/maven/bundles/external"]
jboss = "http://repository.jboss.org/maven2/"
javaNet = "http://download.java.net/maven/2/"

grailsVersion = '1.3.7'

allprojects {
	group = 'EXT.DOMAIN.cpe'
	version = '0.8.10-SNAPSHOT'

	repositories {
    	mavenRepo urls:nexus
		mavenRepo urls:springEbr
		mavenRepo urls:[jboss,javaNet]
		mavenCentral()
	}
}
subprojects {
	slf4jVersion = '1.5.10'
	servletVersion = '2.5'
	springVersion = '3.0.5.RELEASE'
	springSecurityVersion = '3.0.4.RELEASE'
 	junitVersion = '4.8.1'
	easymockVersion = '3.0'
}

// configure java subprojects
configure(subprojects.findAll { !it.name.contains('grails')} ) {
	apply plugin: 'java'
    apply plugin: 'maven'
	apply plugin: 'project-report'

	sourceCompatibility = 1.6
	targetCompatibility = 1.6
	
	task sourcesJar(type: Jar, dependsOn:classes) { 
	     classifier = 'sources' 
	     from sourceSets.main.allSource 
	} 

	task javadocJar(type: Jar, dependsOn:javadoc) { 
	     classifier = 'javadoc' 
	     from javadoc.destinationDir 
	} 

	artifacts { 
		archives sourcesJar
	 	archives javadocJar
	}
	
	jar {
	    manifest {
	        attributes 'Implementation-Version': version,
						'Implementation-Vendor': 'US Department of Veterans Affairs'
	    }
	}

    configurations {
        deployerJars
    }

    dependencies {
        deployerJars "org.apache.maven.wagon:wagon-ssh:1.0-beta-2"
    }

    uploadArchives {
        repositories.mavenDeployer {
            configuration = configurations.deployerJars
            repository(url: "scp://repos.mycompany.com/releases") {
                authentication(userName: "me", password: "myPassword")
            }
            snapshotRepository(url:"scp://repos.mycompany.com/snapshots") {

            }
        }
    }
}
