
buildscript {
    repositories {
        mavenRepo name: 'name', urls: "http://vhaislbll22.vha.DOMAIN.EXT:8081/nexus/content/groups/public/"
        mavenRepo name: 'sonatype', urls: "https://oss.sonatype.org/content/groups/public"
    }
    dependencies {
        classpath group: 'org.gradlefx', name: 'gradlefx', version: '0.2.4'
    }
}

nexus = "http://vhaislbll22.vha.DOMAIN.EXT:8081/nexus/content/groups/public/"
springEbr = ["http://repository.springsource.com/maven/bundles/release", "http://repository.springsource.com/maven/bundles/external"]
jboss = "http://repository.jboss.org/maven2/"
javaNet = "http://download.java.net/maven/2/"
sonatype = "https://oss.sonatype.org/content/groups/public"

description = 'CPE Health-Time Modules'

apply plugin: 'idea'

allprojects {
    group = 'EXT.DOMAIN.cpe'
    version = '2.0.18-SNAPSHOT'

    apply plugin: 'project-report'

    repositories {
        mavenRepo urls: nexus
        mavenRepo urls: springEbr
        mavenRepo urls: [jboss, javaNet]
        mavenCentral()
    }
}
subprojects {
    slf4jVersion = '1.5.10'
    servletVersion = '2.5'
    springVersion = '3.0.5.RELEASE'
    springSecurityVersion = '3.0.4.RELEASE'
    hibernateVersion = '3.3.1.GA'
    solrVersion = '3.3.0'
    junitVersion = '4.8.1'
    easymockVersion = '3.0'
}

// configure java subprojects
configure(subprojects.findAll { !it.name.contains('grails') && !it.name.contains('as')}) {
    apply plugin: 'java'
    apply plugin: 'maven'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    jar {
        manifest {
            attributes 'Implementation-Version': version, 'Implementation-Vendor': 'US Department of Veterans Affairs'
        }
    }

    configurations {
        deployerJars
    }

    dependencies {
        deployerJars "org.apache.maven.wagon:wagon-ssh:1.0-beta-2"
    }

    uploadArchives {
        repositories.mavenDeployer {
            configuration = configurations.deployerJars
            repository(url: "scp://repos.mycompany.com/releases") {
                authentication(userName: "me", password: "myPassword")
            }
            snapshotRepository(url: "scp://repos.mycompany.com/snapshots") {

            }
        }
    }
}

// configure flex subprojects
configure(subprojects.findAll { it.name.contains('as')}) {
    apply plugin: 'gradlefx'

    output = "${project.name}-${version}"

    srcDirs = [
            '/src/main/flex'
    ]

    testDirs = [
            '/src/test/flex'
    ]

    additionalCompilerOptions = [
            '-target-player=10.0.0',
            '-accessible=true',
            '-headless-server=true',
            '-locale=en_US',
            '-debug=true',
            '-keep-as3-metadata+=Bindable,Event,Managed,ChangeEvent,NonCommittingChangeEvent,Transient,Inject,Autowire,Mediate,Dispatcher,PostConstruct,PreDestroy'
    ]
}
